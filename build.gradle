plugins {
    id 'io.codearte.nexus-staging' version '0.11.0'
}

dependencies {
    compile "de.otto:hmac-auth-server:2.2.0"
    compile "org.springframework.boot:spring-boot:1.5.9.RELEASE"
    compile 'org.springframework:spring-web:4.3.14.RELEASE'
}

nexusStaging {
    username=sonatypeUsername
    password=sonatypePassword
    packageGroup='de.otto'
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'eclipse'

    version = '0.2.1'
    group = 'de.otto.edison'

    repositories {
        mavenLocal()
        mavenCentral()
        maven { url 'https://oss.sonatype.org/content/repositories/releases/' }
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
    }

}

apply plugin: 'maven'

jar {
    manifest.attributes provider: 'gradle'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

apply plugin: 'signing'

signing {
    sign configurations.archives
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }
            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            pom.project {
                name 'edison-hmac'
                packaging 'jar'
                description 'A library used at otto.de to integrate hmac into edison microserivces.'
                url 'http://github.com/otto-de/edison-hmac'

                scm {
                    url 'scm:git@github.com:otto-de/edison-hmac.git'
                    connection 'scm:git@github.com:otto-de/edison-hmac.git'
                    developerConnection 'scm:git@github.com:otto-de/edison-hmac.git'
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'gsteinacker'
                        name 'Guido Steinacker'
                    }
                    developer {
                        id 'mbargmann'
                        name 'Matthias Bargmann'
                    }
                    developer {
                        id 'fbregulla'
                        name 'Frank Bregulla'
                    }
                    developer {
                        id 'ftorkler'
                        name 'Florian Torkler'
                    }
                    developer {
                        id 'miregel'
                        name 'Micha Regel'
                    }
                    developer {
                        id 'hstruebe'
                        name 'Henning Strueber'
                    }
                    developer {
                        id 'sweiler'
                        name 'Simon Weiler'
                    }
                }
            }
        }
    }
}



